// Coverage (Cobertura) ////////////////////////////////////////////////////////
// Run as: gradle testGlobal coberturaReport performCoverageCheck

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.2'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
    }
}

// Reference by FQCN instead of name: http://forums.gradle.org/gradle/topics/plugin_not_found_when_i_using_a_gradle_from_other_gradle_file
apply plugin: net.saliman.gradle.plugin.cobertura.CoberturaPlugin
apply plugin: org.kt3k.gradle.plugin.CoverallsPlugin

subprojects {
    apply plugin: net.saliman.gradle.plugin.cobertura.CoberturaPlugin
}

cobertura {
    coverageFormats = ['html', 'xml']
    coverageReportDatafile = file("build/cobertura/cobertura.ser")
    coverageReportDir = file("build/reports/coverage")

    coverageDirs = []
    coverageSourceDirs = []
    coverageMergeDatafiles = []

    rootProject.subprojects.each {
        coverageFormats = ['html', 'xml']
        coverageDirs << file("${it.buildDir}/classes/main")
        coverageSourceDirs << file("${it.projectDir}/src/main/java")
        coverageMergeDatafiles << file("${it.buildDir}/cobertura/cobertura.ser")
    }
}

coveralls {
    coberturaReportPath = "${buildDir}/reports/coverage/coverage.xml"
}